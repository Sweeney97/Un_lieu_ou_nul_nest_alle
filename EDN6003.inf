
Constant Story "Une pi@`ece o@`u nul n'est all@'e";
Constant Headline "^Antoine Sweeney^";
Constant MANUAL_PRONOUNS;
!Constant MAX_CARRIED = 1;

Include "Parser";

Object LibraryMessages
            with before  [;
                                                if (lm_n == 30) "Il y a des choses plus importantes @`a faire.";
                                 ];

Include "VerbLib";

! Vérifier la cohérence générale de l'histoire

!----------------------------------Routine initiale--------------------------------------------!

[ Initialise;
    player.description = "Vous vous reconnaissez, mais quelque chose @`a propos de vous semble diff@'erent. Vous avez de la difficult@'e @`a mettre le doigt dessus... Vous semblez plus lumineux qu'@`a l'accoutum@'ee.";
    give player light;
    lookmode = 2;
    location = garden;
    style underline; print "^Laissez-moi vous conter une histoire qui se d@'eroule il y a bien longtemps, autrefois, enfouis dans une brume amplifi@'ee par les mythes et les ragots, colport@'ee par les murmurateurs et les blasph@'emateurs, cependant encens@'ee par les pr@^etres et les moines.
    Dans la petite ville de Sacerdoce, qui devait sa r@'eputation @`a son @'eglise belle comme nulle autre, pour laquelle des milliers de p@`elerins gravissaient le mont Rougeoyant et la vall@'ee De Joachim, il y avait un orphelinat tristement c@'el@`ebre.
    Des enfants de plusieurs villes avoisinantes y @'etait @'econduits, et les habitants le surnommait, h@'elas, la maison des pleurs.
    Un flot d'@^ames transita par la maison des pleurs, mais l'histoire la plus conn@^ut de tous est l'histoire de ~la fille de Dieu~.
    Cette petite, comme bien d'autres, n'avait ni p@`ere ni m@`ere, et ignorait tout d'eux.
    Seulement, au contraire des autres, elle n'avait pas de nom; ce qui lui valut d'@^etre la cible d'insultes de la part des enfants.
    Un soir, les enfants firent un feu dans l'arri@`ere-cours.
    La fillette rentra tout essouffl@'ee, couverte de cendre, ce qui alarma les s@oeurs.
    Elle resta muette, cependant un @'ev@`enement semblait l'avoir marqu@'ee.
    Les s@oeurs couch@`erent tous les enfants, @'eteignirent le feu, ne pouvant obtenir d'aucun gredin une seule r@'eponse.
    Ce soir-l@`a, la petite s'endormit bien t@^ot, et les s@oeurs pri@`erent pour elle, craignant pour sa sant@'e; elle perlait de sueur, se tordait et g@'emissait de douleur dans son sommeil, sa petite face ronde semblait contrari@'ee par quelque douleur invisible.

    D@'esormais, laissez-moi vous conter comment cette @'etrange histoire se termina apr@`es cette longue nuit...^^";
    style roman; print "Vous vous r@'eveillez, @'etal@'e sur l'herbe ti@'edit par une lumi@`ere blanche. Vous prenez un instant pour vous relever, et examiner les lieux.^^";
 ];

!-----------------------------classes-------------------------------------------------!

Class Deco
  with  before [;
          Take,Pull,Push,PushDir:
            print_ret "Vous voulez red@'ecorer?";
        ],
   has scenery;

Class Furniture
  with  before [;
          Take,Pull,Push,PushDir:
            print_ret "Cela semble inutile.";
        ],
  has   scenery supporter;

Class Livre
    with  before [;
                                read: <<examine self>>;
                                Take,Pull,Push,PushDir:
                                            print_ret "Cela n'est pas n@'ecessaire.";
                         ],
    has scenery;

Class Npc
  has animate proper;

Class Room
 with   before  [;
                            listen: if (doll hasnt general) print_ret "Vous pouvez entendre les plaintes lacinantes d'Anastasia.";
                       ],
  s_to "Vous vous approchez du vide, perdez pied, et reculez subitement.",
  n_to " Derri@`ere se trouve un immense mur. Aucun int@'er@^et donc, @`a aller par l@`a.";

!--------------------------------Lieux----------------------------------------------!
Room garden "Le jardin"
    with description     [;
                              print  "Vous @^etes dans un petit jardin, bien coquet. Pr@`es de vous, une table en fer forg@'e et deux chaises en osier semblent attendre deux causeurs prenant un rafra@^ichissement @`a l'ombre d'un saule. Le jardin contient toutes sortes de fleurs color@'ees: des tulipes, du muguet, des pens@'ees, des violettes, et des g@'eraniums. Le jardin est contourn@'e par une haute cl@^oture peinte en noir. @`A l'Est, vous pouvez apercevoir une maisonnette, et une entr@'ee donnant sur un salon.";
                              if (fox notin garden) print "^Un pichet de limonade est pos@'e sur la table du jardin.";
                              if (appearance1 in garden) print "^Une fillette @'etait d@'esormais dans le jardin, assise par terre. Elle rigola @`a votre arriv@'ee.";
                                   ],
            before  [;
                                go: if (noun == e_obj && fox in garden) print_ret "Vous le laisseriez en plan ainsi, alors qu'il vous a affectueusement apport@'e un breuvage?";
                                      if (noun == e_obj && appearance1 in garden) print_ret "Vraiment? Quelle impolitesse! Allez la voir, au moins.";

                         ],
    w_to "Une cl@^oture de m@'etal noir contourne le jardin.",
    e_to lounge;

  Furniture table "table" garden                                 !remplissage jardin
    with description "Une table de fer forg@'e, peinte en noir. Son dessus est grillag@'e.",
            name 'table';

     Furniture chaise "chaise" garden
      with description "Deux chaises en osier beige naturel.",
            name 'chaises//p' 'chaise';

      Deco fleurs "fleurs" garden
      with description "Les fleurs sont dispos@'ees par couleur, et ne souffrent d'aucune imperfection; ce doit @^etre un jardinier bien amoureux de ces plantes qui les entretient.",
            name 'fleurs' 'violettes' 'pensees' 'geraniums' 'muguet' 'muguets' 'tulipes',
      has pluralname;

      Object maisonnette "maisonnette" garden
      with description "Du jardin, vous pouvez voir un @oeil de b@oeuf @`a l'@'etage, et au rez-de-chauss@'ee un salon garni de biblioth@`eques. La face Est est compos@'ees de planches de bois peintes en blanc.",
            name 'maisonnette' 'maison' 'entre' 'entree' 'salon',
      has scenery female;

       Object cloture "cl@^oture" garden
      with description "Une cl@^oture de m@'etal noir. Derri@`ere, vous apercevez un immense mur brun.",
            name 'cloture',
      has scenery female;

       Object saule "saule" garden
      with description "Un saule mature aux feuilles ovales et vertes.",
            name 'saule' 'arbre',
      has scenery;

        Deco buisson "buisson" garden
              with description "Ce buisson fait bien la taille d'un homme.",
                      name 'buisson' 'arbuste',
                      before  [;
                                       enter, search: print_ret "Ah @cca! laissez le renard tranquile!";
                                ],
              has scenery;

Room lounge "salon"
    with description [;
     print "Le parquet en bois du salon est mouill@'e de larmes. Vous devinez la propri@'etaire de cette maison ais@'ee, puisque l'int@'erieur est apparemment luxueux.
    Des biblioth@`eques vitr@'ees de style @'elisab@'ethain occupent le fond de la pi@`ece. Des lambris plaqu@'es de bois d'acajou recouvrent les murs @`a mi-hauteur, compl@'et@'e par des tissus tendu aux motifs floraux. Le plafond @`a caissons est prolong@'e dans des culots en feuilles d'acanthe et un foyer de pierre dont le linteau sculpt@'e illustre une sc@`ene de beuverie se trouve @`a l'entr@'ee.

    @`A l'Ouest, vous pouvez retourner dans le jardin. @`a l'Est, vous sentez l'odeur d'une cuisine. Un escalier, monte au premier @'etage.";
    if (doll hasnt general) print " Des larmes descendent en cascades l'escalier, accompagnant les plaintes horribles d'Anastasia.";
                                ],
            before  [;
                                  smell: print_ret "@cCa sent les @'epices!";
                         ],

    w_to garden,
    e_to kitchen,
    u_to chamber;

   Object parquet "parquet" lounge                     ! remplissage salon
             with description "Le parquet est compos@'e de tuiles de bois carr@'ees, repr@'esentant chacune une fleur.",
                     name 'sol' 'parquet',
            has scenery;

    Furniture bibliotheque "biblioth@`eques" lounge
             with description  [;
              print "Il y a cinq biblioth@`eques au total. Chacune contient une cinquantaine de livres. Leurs vitres sont impeccables, signe d'un entretient soign@'e. ^Vous identifiez trois livres qui pourraient vous int@'eresser.^Un recueil de po@`emes.^Un trait@'e sur les maisons de poup@'ees.^Un livre d'analyse sur les contes de Langelier.";
              style underline; print "^^[Pour lire le trait@'e, @'ecrivez ~lire traite~. Pour lire le recueil, @'ecrivez ~lire recueil~. Pour lire le livre d'analyse, @'ecrivez ~lire livre~.]";
                                       ],
                     name 'bibliotheques//p' 'bibliotheque',
                    before  [;
                                      search:   <<examine self>>;
                                      open:     <<examine self>>;
                                 ],
              has female pluralname;

    Deco lambris "lambris" lounge
             with description "Les lambris s'@'el@`event @`a mi-hauteur du mur, et sont plaqu@'e de bois d'acajou.",
                   name 'lambris',
              has pluralname;

    Deco tissus "tissus" lounge
             with description "Les tissus sont tendus au mur, et occupent les murs du plafond aux lambris. Des motifs floraux y sont imprim@'es.",
                   name 'tissus',
             has pluralname;

     Deco culot "culots" lounge
             with description "Les culots sculpt@'es en feuille d'acanthe prolongent les encoignures du plafond @`a caissons.",
                   name 'culots//p' 'culot';

       Object plafon "plafond" lounge
             with description "Les caissons du plafond sont d'environ d'un pied de profondeur par un pied de largeur.",
                   name 'plafond' 'caissons',
              has scenery;

       Furniture foyer "foyer" lounge
             with description "Le foyer de pierre comporte deux corbeaux sculpt@'es en forme de pans. Le linteau repr@'esente une Dionysies et ses buveurs.",
                   name 'foyer' 'linteau';

         Object escalierhaut "escalier" lounge
             with description "L'escalier est trempe. Il monte directement @`a l'@'etage.",
                   name 'escalier//p' 'escalier',
              has scenery;

Room kitchen "cuisine"
    with description "La cuisine respire peu; tout son espace est occup@'e, des pieds au plafond. Un support @`a casseroles suspendu et bin rempli emp@^eche une vision d'ensemble. Les cimaises qui courent les murs au niveau du torse sont remplies de pot @`a @'epices de toutes les couleurs. Un ilot en marbre, situ@'e sous le support suspendu, semble pr@^et @`a accueillir une arm@'ee de cuistot. Au fond, un four @`a bois est entour@'e de multiples armoires. La porte du cellier est @`a l'Est. Vous pouvez retourner au salon en allant vers l'Ouest.",
            before  [;
                            listen: if (appearance2 in armoire) print_ret "Vous entendez une enfant rire.";
                            smell: print_ret "@cCa sent les @'epices!";
                         ],
    e_to woodendoor,
    w_to lounge;

  Deco support "support suspendu" kitchen   !remplissage cuisine
    with description "Un support de m@'etal suspendu, auquel sont accroch@'ees une dizaine de casseroles.",
            name 'support' 'suspendu';

      Deco casseroles "casserole" kitchen
          with description "Des casseroles en fonte de toutes les tailles.",
                  name 'casseroles//p' 'casserole';

      Furniture ilot "@^Ilot" kitchen
           with description "Un imposant ilot de marbre, qui occupe le centre de la pi@`ece.",
                   name 'ilot';

      Furniture cimaise "cimaises" kitchen
                with description "Des cimaises de bois qui courent les murs, et assez large pour supporter nombre d'@'epices.",
                        name 'cimaises//p' 'cimaise';

      Furniture epices "@'epices" kitchen
                with description "Gingembre, curcuma, clou de girofle, muscade, cannelle, vanille du Mexique, poivre noir, poivre de cayenne, marjolaine, etc.",
                        name 'epices//p' 'epice';

        Object four "four a bois" kitchen
                with description "Un four @`a bois en pierre, avec une cloison en fer. Dans la bouche, vous apercevez des b@^uches pr@^etes @`a l'usage.",
                        name 'four' 'bois',
                has scenery container open;

        Object armoire "armoires" kitchen
    with description "Une armoire",
            name 'armoires//p' 'armoire',
            before  [;
                                open: if (appearance2 in armoire)  {
                                          give armoire open;
                                          move appearance2 to kitchen;
                                          print "Elle sortit de l'armoire, amus@'ee. Vous faites mal aux yeux! Je ne savais pas que les gens pouvaient @'eclairer si fort! Vous @^etes venu me chercher pour que je parte l@`a-haut? J'aimerais bien @^etre au paradis! Les bonnes s@oeurs m'en parlent tout le temps. Ah ce que j'y serai bien! Et Anastasia y sera! Et je pourrai peindre tous les anges! Dites, en @^etes-vous un? Les s@oeurs m'ont dit que les anges avaient une t@^ete qui @'eclairait.";
                                          return true; }
                         ],

    has container scenery openable;

Room chamber "chambre"
    with description "La chambre ne communique avec aucune pi@`ece @`a l'@'etage. Un petit brasero romain repose pr@`es du lit pour les nuits froides. Sur un gu@'eridon, un ensemble de th@'e de porcelaine chinoise au marli dor@'e de dragons d'Asie attend patiemment ses buveurs. Accot@'e sur le mur, un r@'ecamier poussi@'ereux donne l'impression d'une chambre abandonn@'ee. Sur une console, une torch@`ere aux bougies fondues n'@'eclaire plus depuis longtemps; et, tout pr@`es, un bateau de porcelaine repose. Malgr@'e tout, la chambre garde une impression a@'er@'ee, en raison d'un @oeil de b@oeuf qui compl@'etait le mur Ouest. De plus, le plafond se prolongeait en une coupole repr@'esentant un ciel bleu-poudre avec quelques nuages, et dont le tambour @'etait couvert de ch@'erubins. Pr@`es du lit, une armoire au panneau aux vantaux sculpt@'e en visage antique compl@'etait le mobilier de la chambre.",
    d_to lounge;

    Deco brasero "brasero" chamber            ! remplissage chambre
        with description "Trois femmes romaines forment le tripode du brasero",
                name 'brasero';

          Furniture lit "lit" chamber
          with description "Un lit @`a l'@'edredons de soie rose et bleue.",
                  name 'lit';

        Deco ensemblethe "ensemble de th@'e" chamber
            with description "Un ensemble de the d@'e porcelaine chinoise. Outre le marli dor@'e de dragons d'Asie, des sinogrammes sont peints au centre des soucoupes.",
                    name 'ensemble' 'the' 'tasses' 'soucoupes';

        Furniture gueridon "gu@'eridon" chamber
            with description "Une table ronde et de pierre blanche.",
                    name 'gueridon' 'table';

        Furniture recamier "r@'ecamier" chamber
            with description "Le r@'ecamier, au tissu rose-d@'elav@'e, est visiblement poussi@`ereux.",
                    name 'recamier' 'sofa' 'divan';

        Furniture console "console" chamber
            with description "Une console en bois fonc@'e, avec un dessus en marbre.",
                    name 'console',
            has female;

        Deco torchere "torchere" chamber
            with description "Une torch@`ere de m@'etal, sans fioritures.",
                    name 'torchere',
            has female;

        Deco bateau "bateau de porcelaine" chamber
            with description "Vous observez les d@'etails du bateau de porcelaine. Cet objet si fin, si d@'elicat ne peut @^etre l'@oeuvre que d'un grand ma@^itre, pensez-vous.",
                    name 'bateau';

        Deco fenetre "fen@^etre" chamber
            with description "Une fen@^etre ronde, par laquelle vous pouvez appr@'ecier la vue sur le jardin.",
                    name 'fenetre' 'oeil' 'boeuf',
                    before  [;
                                        open: print_ret "Elle est d@'ej@`a ouverte.";
                                        close: print_ret "Impossible, il faut laisser de l'air pour cette pauvre Anastasia.";
                                 ],
            has female;

        Furniture armoirevantail "armoire aux vantaux sculpt@'es" chamber
            with description "Vous regardez de plus pr@`es les visages, ils semblent indiquer deux expressions contraires. L'un la joie, l'autre la tristesse, comme les masques de th@'e@^atre.",
                    name 'armoire//p' 'armoire' 'vantaux' 'vantail',
                    before  [;
                                        open: print_ret "Pourquoi voulez-vous fouiller?";
                                        close: print_ret "L'armoire est d@'ej@`a ferm@'ee.";
                                 ],
            has female;

        Deco coupole "coupole" chamber
            with description "La coupole prolonge le regard qui s'@'el@`eve vers le plafond, par un ciel bleu-poudre et quelques nuages fins.",
                    name 'coupole' 'plafond',
            has female;

        Deco tambour "tambour" chamber
            with description "Le tambour est couvert de petits ch@'erubins.",
                    name 'tambour',
                    before  [;
                                        examine: <<examine cherubins>>;
                                 ];

        Deco cherubins "ch@'erubins" chamber
            with description "Les ch@'erubins volent en toutes directions, et sont, fid@`eles @`a leur image habituelle, bien dodus.",
                    name 'cherubins//p' 'cherubin';

Room cellar "cellier"
    with description " Le cellier est en pierres d@'egrossies, avec quelques @'etag@`eres portant leurs bouteilles de vin et leurs victuailles.^
                                 Plusieurs animaux @'etaient l@`a: des oiseaux volaient en cercles serrer au plafond, des chats c@^otoyaient des cygnes, des chiens, des loups, des lapins, des cochons, des vaches, des veaux, des moutons, une dinde, des @^anes, des renards, des cerfs, des @'ecureuils.",
             before  [;
                               open: if (noun == heart) <<enter heart>>;
                               enter:   if (noun == heart && self has general) print_ret "Inutile d'y retourner.";
                                           if (noun == heart && tears in player) { print "Aucun son, aucune odeur, seul le vide. Tu erres quelques instants, perdus, sans rep@`ere. Soudain, un rire d'enfant @'eclaire les t@'en@`ebres. C'est le gazouillis de la vie, aussi doux que ceux des oiseaux. Un rayon de lumi@`ere @'eclaire une enfant naissante. Un ange glisse dans le rayon jusqu'@`a l'enfant et le regarde avec tendresse. Il dit: ~Tu vivras, pauvre enfant, des nuits de malheur et de solitude. Tu seras seule, mais sache que tu sein de Dieu tu naquis, au sein de Dieu tu retourneras. Peut-@^etre pense-tu n'avoir ni destin ni famille, mais Dieu serait pour toi et l'un et l'autre. Ton nom sera donc @'elise Videre, car tu as vu Dieu, et re@ccois en @'echange le don de la vision. @'elise, va, nous nous retrouverons. ~ L'ange se dissipa comme un nuage souffl@'e par un vent paisible. Et le noir fut. Ton visage retrouva sa luminosit@'e, et @'eclaira de nouveau le cellier, o@`u @'Elise reposait au centre de ses animaux.";
                                           give self general;
                                           return true; }
                                           if (noun == heart && tears notin player) print_ret "Il vous manque un peu de tristesse pour accomplir cette action.";
                          ],

    w_to woodendoor;

  Furniture etageres "@'etag@`eres" cellar     ! remplissage cellier
        with description "Des @'etag@`eres en vieux bois craqu@'e et fendu de toute part. ",
                name 'etageres',
        has pluralname;

    Object vivres "vivres" cellar
        with description "Du fromage de Sacerdoce, des navets, des choux, des patates, des viandes sal@'ees.",
                name 'vivres' 'victuailles' 'aliments',
        has pluralname scenery;

      Object bouteilles "bouteille" cellar
          with description "Rien de bien distingu@'e: seulement quelques bonnes bouteilles du vignoble de Saint-Antoine.",
                  name 'bouteilles//p' 'bouteille' 'vin',
          has female scenery;

Object heart "In petto" cellar
    with description "Une noirceur d'une rare intensit@'e. Peut-@^etre devriez-vous sortir?",
            name 'coeur' 'ame',
            has container enterable open scenery;

Object woodendoor "porte du cellier"
    with description  [;
                                  if (woodendoor has open) print "La porte du cellier est entreb@^aill@'ee.";
                                  if (woodendoor hasnt open) { print "La porte du cellier est verrouill@'ee, et vous n'avez nulle envie de recherche la maison pour une clef.";
                                  print " Pour une porte de cellier, elle est bien d@'etaill@'ee pensez-vous. Les montant en m@'etal sont sculpt@'es en lierres, alors que le linteau porte l'inscription suivante:";  style underline; print "video meliora proboque, deteriora sequor.";
                                  style roman; print "Les @'ecoin@ccons repr@'esentent des anges soufflant dans leur trompette."; }
                                  ],
    name 'porte' 'bois' 'en',

    door_to [;
        if (self in kitchen) return cellar; return kitchen;
                  ],
    door_dir [;
          if (self in kitchen) return e_to; return w_to;
                  ],
    found_in cellar kitchen,
    has scenery door openable locked female;

!-------------------------objets---------------------------------------------------!

Livre livre1 "Trait@'e sur les maisons de poup@'ees allemandes" lounge
    with description "Vous feuilletez le livre, et lisez ce passage: ~Les maisons de poup@'ees allemandes sont les mieux r@'eput@'ees pour leur qualit@'e. Harnold & Shwartz est g@'en@'eralement reconnue comme @'etant la marque ayant le mieux tir@'e son @'epingle du jeu sur un march@'e de plus en plus comp@'etitif, en raison de l'engouement croissant des pays europ@'eens pour ces objet. @`a l'origine, H. & S. construisait des maisons de poup@'ees pour adulte, symbole de richesse, genre de troph@'ee extr@^emement dispendieux. C'est seulement quelques d@'ecennies plus tard que les maison de poup@'ees pour enfant se popularis@`erent. @'Etant donn@'e des co@^uts de fabrication encore aujourd'hui @'elev@'e, elle sont plut@^ot destin@'ees @`a la bourgeoisie, et symbolisent encore une certaine richesse. Les mod@`eles H. & S. ont g@'en@'eralement des fa@ccades mont@'ees sur gonds, au contraire de leur rival Britannique Toys of London, qui opte pour des mod@`eles @`a fa@ccades ferm@'ees et d@'ecor@'ees.~",
            name 'traite' 'maison' 'poupees';

Livre livre2 "Du conteur dans les contes de Langelier" lounge
    with description "Vous feuilletez le livre, et lisez ce passage: ~Le conteur dans les contes de l'@'ecrivain Henry Langelier se tient sur la mince ligne entre personnage et narrateur.
    Parfois il se permet des commentaires sur l'action, toujours plein d'ironie, de malice. Toujours fulgurant, entre deux lignes, per@ccant la surface m@'etatextuelle, il parvient @`a insuffler aux contes cette force vitale qui font de Langelier un des auteurs les plus intrigants de son si@`ecle.
    C'est particuli@`erement dans le conte ~La bouche pleine de pierreries~ que son r@^ole est int@'eressant @`a analyser. Dans cette histoire, Ariane entend les commentaires du conteur, ce qui brise la barri@`ere g@'en@'eralement @'etablie par convention dans les contes entre l'univers racont@'e et le raconteur. Non seulement Ariane entend-elle les commentaires, mais un v@'eritable dialogue prend place entre elle et le conteur. Elle lui parle, et le d@'esigne comme @'etant ~le conteur~, ce dernier avouant ne pas avoir v@'eritablement de nom. L'absence de nom n'est pas un d@'etail anodin: le conteur incarne sa fonction et ne poss@`ede par cons@'equent pas d'existence propre @`a l'ext@'erieur des limites de l'univers fictionnel. En s'adressant directement au conteur, Ariane met @`a nu un proc@'ed@'e de l'@'ecriture qui se d@'evide sous les yeux du lecteur.~",
              name 'langelier' 'livre';

Livre livre3 "recueil de po@'esie" lounge
    with description " Vous feuilletez le recueil, et lisez ce po@`eme:^^

  ~Il faut savoir cueillir les pleurs^
    Comme la ros@'ee au jour levant^
    Lorsqu'ils reposent au pied du pleureur^
    Et s'en servir pour ouvrir ce qui r@'eside dedans^
    Une @^ame @'epuis@'ee par les vicissitudes ^
    Pr@^ete @`a embrasser la vastitude~^",
            name 'recueil' 'poeme' 'poemes' 'poesie';

! Si vous écrivez lire "note cachee" ou "lire note", vous pourrez lire la note cachée, qui contient le nom de la petite.
Livre note "Note cach@'ee" lounge
    with description "Oh, je parie que vous avez lu @cca quelquepart, mais dites-vous que @cca n'existe pas, les solution facile et sans efforts. Bien essay@'e, tout de m@^eme.",
            name 'note' 'cachee';

Object limonade "limonade" garden
    with description "Une limonade pr@'epar@'ee par le renard.",
            name 'boisson' 'limonade' 'citronade' 'rafraichissement' 'breuvage' 'decoction' 'pichet',
            before  [;
                              drink: if (fox in garden) {
                              print " Vous vous servez un peu de limonade. Le renard engagea la conversation.^
                              J'imagine que vous @^etes venu rendre visite @`a la fille sans nom? Elle ne se montrer pas aussi ais@'ement. Vous devrez probablement aller voir Anastasia, dans la maisonnette @`a l'Est, @`a l'@'etage du haut, avant d'obtenir l'autorisation. C'est une v@'eritable madeleine, vous ne pourrez la manquer, parole de canid@'e! Depuis quelque temps, les journ@'ees sont mornes, @`a n'en pas douter, elles le seront encore longtemps! Pauvre fille, ah Seigneur. Vous savez, je suis chanceux d'avoir @'echapp@'e @`a cette h@'ecatombe. Je suis le seul @`a y avoir @'echapper, et c'est pourquoi vous me voyez assis devant vous aujourd'hui! Je dois cette chance @`a Dieu. Dites-moi, je ne vous emb@^ete pas trop avec mes histoires? Ce fut une nuit terrible vous savez, l'odeur de la fum@'ee restera longtemps dans ma petite noix, fit-il en se cogna le cr@^ane. Ils ont tout br@^ul@'e. Ces mioches n'ont aucun respect. Ce n'@'etait pas facile pour eux de la voir si habile, peinturant comme si c'eut @'et@'e une rigolade, avec tant d'aise. Elle n'avait pas son pareil pour les plumes et les pelages, et tout Sacerdoce accourait pour voir la faune qu'elle immortalisait! Ah, soupira-t-il apr@`es une grande lamp@'ee de limonade, ce dut @^etre la jalousie. Elle corrompt tout ce qu'elle touche, celle-l@`a. Ils ne supportaient pas, les petits, de n'@^etre plus le centre de l'attention. Il n'y avait de visite que pour elle seule, que pour ses peintures. Au Diable le reste! Les enfants @'etaient condamn@'es @`a s'accrocher aux jupons pour r@'ecolter une once d'attention, une tape sur les fesses, ou un soufflet d'irritation! Je les comprends aussi, cette douleur qu'ils devaient ressentir, @^etre abandonn@'e blesse tout un chacun, m@^eme la deuxi@`eme fois, conclu-t-il.
                              D@`es qu'il eut termin@'e son monologue, un son per@ccant vrilla vos oreilles. Un cri, suivit d'une longue plainte, et de sanglots interminables.
                              Bon, voil@`a Anastasia qui repart son @'eternelle complainte. Je vous laisse, fit-il en regardant sa montrer, puis il se faufila @`a nouveau dans le buisson d'o@`u il @'etait apparu. ~ Adieu! et bonne chance, on ami! ~ dit le Renard. Vous vous levez de la chaise.";
                              remove fox;
                              return true; }
                              if (fox notin garden) print_ret "Vous prenez un verre de limonde. Elle est, en v@'erite, parfaitement sucr@'ee et bien rafra@^ichissante!";
                              take: print_ret "Pourquoi donc? C'est bien mieux de la boire que de la mettre dans ses poches!";
                              push: print_ret "Pour la faire tomber par terre?";
                         ],
    has concealed;

Object tears "Larmes"
    with description "Des joyaux bleu-tristesse.",
            name 'larmes' 'pleurs' 'pleur' 'larme',
            before  [;
                             take: print "Vous vous penchez pour recueillir les larmes au sol. Vous les mettez soigneusement dans votre poche.";
                                      move tears to player;
                                      return true;
                         ],
    found_in chamber lounge,
    has concealed pluralname;

!----------------------------Npc--------------------------------------------------!

Npc fox "renard" garden
    with initial [;
                         "Un renard surgit d'un buisson, et se posa contre vous, en grognant. Stup@'efait et saisi de terreur, vous ne pouvez bouger d'un seul pouce. Apr@`es un moment, il cessa de glapir. ~ Je vous prie de m'excuser, se plaignit le renard. Il faisait tout noir et d'un coup, toute cette lumi@`ere... vous @^etes lumineux ma parole. Mais en vrai sachez que je n'ai pas l'habitude d'accueillir aussi m@'echamment les h@^otes de notre maison. Vous d@'esirez un peu de limonade? Tenez, asseyez je vous prie. Sans pouvoir glisser ne serait-ce qu'un mot, vous vous assoyez sur la chaise. Le renard trotta nonchalamment vers la maisonnette, et reparut avec un pichet de limonade, l'anse dans la gueule, qu'il vous servit. Il posa le pichet sur la table. ~ Je vous en prie, buvez.~";
                  ],
            description "Un renard au poil roux.",
            life [;
                          "Faites-moi plaisir, buvez un peu ma d@'ecoction, je l'ai pr@'epar@'ee cet apr@`es-midi.";
                  ],
            name 'renard';

Npc doll "Anastasia" chamber
    with description "Elle porte une robe de soie vert-menthe. Ses yeux en boutons de nacre sont gros comme un visage d'homme. En fait, Anastasia fais plusieurs fois votre taille. Tout son corps est gonffl@'e, sa chair est bien rembour@'ee.",
            name 'anastasia' 'poupee',
           times_spoken_to 0,
            initial  [;
                           if (self hasnt general) print "Anastasia pleure une rivi@`ere de larmes. Ses deux boutons d'yeux semble deux puits sans fond. Elle est assise dans sa tristesse, patauge dedans, et prend s@^urement quelque plaisir inavou@'e @`a s'entendre pour ainsi chialer.";
                          if (self has general) print "Anastasia vous sourie gentiment.";
                       ],
            life     [;

                                          kiss, attack, ThrowAt, give, show:
                                          if (self hasnt general) print_ret "Le flot de larmes intarissable vous d@'ecourage d'entreprendre une telle  action. Peut-@^etre devriez-vous la consoler?";
                                          if (self has general)  print_ret "Vous devriez aller trouver la fillette.";
                                          talk: if (self hasnt general)  {
                                          self.times_spoken_to = self.times_spoken_to + 1;
                                          switch (self.times_spoken_to) {
                                           1: "Oh, quoi encore? On m'envoie une veilleuse? L'obscurit@'e n'y est pour rien, je suis triste parce que je suis triste, un point c'est tout! Puis elle se remit @`a pleurnicher. Voyant que vous sembler @^etre l@`a pour rester, elle arr@^eta un peu son tapage. D'o@`u venez-vous, @'etranger? Vous n'@^etes pas d'ici, cela se voit. Oh! ne le prenez pas ainsi, je veux dire, physiquement, nous sommes semblables, mais je parle de vos mani@`eres bien s@^ur. Enfin, peu importe, laissez-moi seule! Et elle repartit de plus belle.";
                                           default: "Laissez-moi!";
                                         }}
                        ;],
            before  [;

                              hug: if (self hasnt general) { print "Oh! @cca alors! elle rendit votre c@^alin. Merci @'enorm@'ement, sanglota-t-elle, en @'epongeant ses larmes. Maintenant je peux vous @'ecouter.^^

                              Comment, vous cherchez la petite fille? Quel malheur, oh Seigneur! Les enfants peuvent @^etre des monstres des fois! Vous savez comment les autres petits l'appelaient? ~La pustule~ puisqu'elle a une petite mouche sur la joue. Rien de bien m@'echant, mais elle l'a tellement entendue qu'un cuistot l'a surpris un soir dans la cuisine en train d'essayer de se l'arracher au couteau! Oh pauvre enfant! Et personne pour la consoler! Personne! C'est moi qui a bu toute ses larmes, peuh peuh! Fit-elle en tirant la langue. Voil@`a pourquoi elles ressortent toutes d@'esormais, j'en ai @`a revendre! Elles me sortent par les oreilles! Et puis, pourquoi voudriez-vous la voir, lumineux @'etranger? Vous venez mettre fin @`a ses souffrances! Oh! Quelle nouvelle!
                              Aurait-on @'ecout@'e mes pri@`eres? Dans ce cas, bien s@^ur que vous pouvez la voir! Oh Seigneur! Bien s@^ur, rench@'erit-elle, presque heureuse d@'esormais. De vous savoir ici me s@`eche enfin les larmes... Oh! @`a pleurer, cela irrite la gorge et les yeux! C'est que j'en pouvais plus, moi, de pleurer! Merci, merci, je vous suis oh! combien redevable! Attendez un peu. D'un bond Anastasia se l@`eve, puis ouvrit la fen@^etre et s'@'epoumona: Il y a quelqu'un pour toi! Cesse de te cacher, n'aie pas peur! C'est une bonne personne! Puis, se retournant vers vous: voil@`a, d@'esormais vous pouvez retourner au jardin, elle y sera. Non, non, je vous en prie! c'est moi qui vous remercie! Je vous inviterais bien en d'autres circonstances @`a prendre le th@'e, mais il y a plus important @`a faire! Quoi? Si cette maison lui appartient? Seigneur! Non, bien s@^ur que non. C'est une donation d'un riche visiteur. Cette maison appartient @`a tous, elle est dans le hall.";
                             give self general;
                             move appearance1 to garden;
                             return true;  }
                             push, take: print_ret "Peut-@^etre devriez-vous la consoler?";
                          ];

Npc appearance1 "fillette"
    with description [;Apparition();],
            name 'fillette' 'enfant' 'petite',
            initial [;
                          print   "Bonjour. Vous venez pour m'adopter? Je pourrai peindre votre portrait, tous les jours! Je suis tr@`es dou@'ee pour la peinture. Les s@oeurs pensent que c'est un don. Quand je peins, je sens que tout est possible. Je me sens lib@'er@'e des murs de l'orphelinat. Ce n'est pas que les s@oeurs sont m@'echantes avec moi! Non, au contraire, elles sont tr@`es bonnes! Je les aime bien. Elles m'ach@`etent plein de choses pour que je puisse peinturer: des canevas, de la peinture des tableaux, j'ai m@^eme un chevalet! Ce sont les autres qui sont m@'echants. Ils me trouvent laides. C'est pour @cca que personne veut me prendre. Mais je sais peindre! Je pourrai peindre votre beau visage! Vous n'avez pas de pustule, vous. Ce que je donnerais pour avoir un visage comme le v@^otre!";
                      ],
            life      [;
                              print "On joue @`a un jeu? Je vais me cacher dans la maison, trouvez-moi!
                              Sur ce, elle s'enfuit en rigolant dans la maison.^^";
                              style underline; print "[Essayez d'@'ecouter son rire afin de trouver la pi@`ece o@`u elle se cache.]";
                              move appearance2 to armoire;
                              remove self;
                              return true;
                             ],
              has female;

Npc appearance2 "fillette"
    with description  [;Apparition();],
            name 'fillette' 'enfant' 'petite',
            life      [;
                              print "Non! Vous ne m'attraperez pas! Je veux rester encore un tout petit peu! Juste le temps de dire au revoir @`a mes amis, au cellier!
                              Ne tenant pas en place, elle rendit au cellier, et laissa la porte ouverte.";
                              remove self;
                              give woodendoor open;
                              return true;
                             ],
                has female;

Npc appearance3 "fillette" cellar
    with description [;Apparition();],
            name 'la' 'pustule',
            life      [;
                              print "Comment? Vous aussi? Vous aussi? cria-t-elle. Sur son visage, des pustules faisaient irruption, et se remplissaient de pus. Ils ne cessait d'en appara@^itre de nouveaux, si bien que son visage en fut couvert, on voyait @`a peine ses yeux. Elle se gratta le visage de ses ongles, comme si elle voulut s'arracher la peau. Sous la pression certains pustules @'eclat@`erent et livr@`erent leur jus blanch@^atre. Elle sembla un monstre, et cette vision horrible fut achev@'ee par les animaux qui flamb@`erent tous d'un coup. L'odeur de leur chair cram@'ee devint insupportable, naus@'eabonde. Ils @'etaient affol@'es: couinaient, hurlaient, glapissait, courant dans tous les sens, brunissant, noircissant, leur oeils devenant liquides, les chairs se ramollissant, fondant, pendant. Au travers ce tohu-bohu cauchemardesque, la petite fille cria: pourquoi, pourquoi vous avez br@^ul@'e toutes mes peintures! Les seules belles choses que j'ai! Et son cris ultime recouvris la sc@`ene d'un voile noir qui @'etouffa tout.";
                              style underline;  print "^^Jamais la petite ne se r@'eveilla. Mais les s@oeurs dirent assister @`a un miracle. Durant la nuit, elle poussa un hurlement terrible. Un rayon de lumi@`ere l'inonda, et un ange descendit. Il la pris d@'elicatement, lui toucha le front. Alors la lumi@`ere p@'en@'etra la t@^ete de la fille, et elle irradia. D@'esormais, dit l'ange qui la tenait dans ses bras, tu seras ~la fille de Dieu~. Puis ils partirent, main dans la main.^^";
                              deadflag = 1;
                              TheEnd();
                              return true;
                      ],
              has female scenery;

Npc appearance4 "@'Elise Videre" cellar
    with description  [;Apparition();],
            name 'elise' 'videre',
            life   [;
                           print "@'Elise vous sourrit timidement. ~Je m'apelle @'Elise? Comme c'est beau! Fit-elle en ouvrant grand ses yeux. Et maintenant, je dois partir? D'accord.~ Elle prit le temps, sereinement, de saluer tous ses animaux. Puis elle vint vous donner un c@^alin.";
                           style underline; print "^^@'Elise rouvrit faiblement les yeux, inond@'e de lumi@`ere. Elle irradiait la pi@`ece, une s@oeur s'@'evanouit. Elle les remercia avec amour, reconnaissante, puis marcha par petit bonds vers le ciel, en gloussant.^^";
                          deadflag = 2;
                          TheEnd();
                          return true;
                     ],
            has female scenery;

Npc intercept "l'enfant" cellar
      with description [;Apparition();],
              name 'enfant' 'petite' 'fille' 'fillette',
              life  [;
                                  print_ret "Vous devez l'appeler par son nom. Le seul qu'elle eut toute son enfance, que lui donnait les autres enfants fera l'affaire. Elle s'y reconna@^itra.";
                      ],
        has female;

Npc fillette "L'enfant endormie" chamber
    with description  [;Apparition();],
      has female scenery;

Npc conteur "conteur"
    with description "Difficile @`a voir, il para@^it si loin, ses traits sont impr@'ecis.",
            name 'conteur' 'au',
            times_spoken_to 0,
            life [;
                  Talk, Ask, Tell: self.times_spoken_to = self.times_spoken_to + 1;
                  switch (self.times_spoken_to) {
                  1: "Ah! vous m'avez d@'ebusqu@'e! C'est difficile de se cacher lorsqu'on est g@'eant comme moi!";
                  2: "Eh oui! Je vous guette depuis votre arriv@'ee ici.";
                  3: "Vous devez aider la petite, c'est votre mission apr@`es tout! C'est pourquoi vous @^etes ici, dans son r@^eve! Vous devez trouver son nom avant qu'elle ne parte.";
                  4: "J'ai envie de conter une histoire qui se termine bien, moi!";
                  5: "S'il vous pla@^it, faites quelque chose!";
                  6: "Peut-@^etre lire les livre vous aidera.";
                  7: "J'adore examiner les petits @^etres se remuer dans cette maison miniature. Je pourrai y passer la journ@'ee! Quoi, vous trouvez cela @'etrange? Allons, nous avons tous, enfant, regard@'e les fourmis!";
                  8: print "Vous voulez"; style underline;  print " r@'eellement "; style roman; print "savoir ou vous @^etes?";
                      if (YesOrNo ()) {
                      print "Vous allez @^etre d@'e@ccus, voil@`a o@`u vous @^etes:^";
                      if (location == kitchen) print_ret "Room kitchen^
                          with description La cuisine respire peu; tout son espace est occup@'e, des pieds au plafond. Un support @`a casseroles suspendu et bin rempli emp@^eche une vision d'ensemble. Les cimaises qui courent les murs au niveau du torse sont remplies de pot @`a @'epices de toutes les couleurs. Un ilot en marbre, situ@'e sous le support suspendu, semble pr@^et @`a accueillir une arm@'ee de cuistot. Au fond, un four @`a bois est entour@'e de multiples armoires. La porte du cellier est @`a l'Est. Vous pouvez retourner au salon en allant vers l'Ouest.,^before  [;^listen: if (appearance2 in armoire) print_ret Vous entendez une enfant rire.;^smell: print_ret @cCa sent les @'epices!;^],^d_to woodendoor,^w_to lounge;^";
                      if (location == garden) print_ret "Room garden^with description [;^print  Vous @^etes dans un petit jardin, bien coquet. Pr@`es de vous, une table en fer forg@'e et deux chaises en pailles semblent attendre deux causeurs prenant un rafra@^ichissement @`a l'ombre d'un saule. Le jardin contient toutes sortes de fleurs color@'ees: des tulipes, du muguet, des pens@'ees, des violettes, et des g@'eraniums. Le jardin est contourn@'e par une haute cl@^oture peinte en noir. @`A l'Est, vous pouvez apercevoir une maisonnette, et une entr@'ee donnant sur un salon.;^if (fox notin garden) print ^Un pichet de limonade est pose sur la table du jardin.;^if (appearance1 in garden) print ^Une fillette @'etait d@'esormais dans le jardin, assise par terre. Elle rigola @`a votre arriv@'ee.;^],^before  [;^go: if (noun == e_obj && fox in garden) print_ret Vous le laisseriez en plan ainsi, alors qu'il vous a affectueusement apport@'e un breuvage?;^if (noun == e_obj && appearance1 in garden) print_ret Vraiment? Quelle impolitesse! Allez la voir, au moins.;^],^w_to Une cl@^oture de m@'etal noire contourne le jardin.,^e_to lounge;^";
                      if (location == lounge) print_ret "Room lounge^with description [;^print Le parquet en bois du salon est mouill@'e de larmes. Vous devinez la propri@'etaire de cette maison ais@'ee, puisque l'int@'erieur est apparemment luxueux.^Des biblioth@`eques vitr@'ees de style @'elisab@'ethain occupent le fond de la pi@`ece. Des lambris plaqu@'e de bois d'acajou recouvrent les murs @`a mi-hauteur, compl@'et@'e par des tissus tendu aux motifs floraux. Le plafond @`a caissons est prolong@'es dans des culots en feuilles d'acanthe et un foyer de pierre dont le linteau sculpt@'e illustre une sc@`ene de beuverie se trouve @`a l'entr@'ee.^@`A l'Ouest, vous pouvez retourner dans le jardin. @`a l'Est, vous sentez l'odeur d'une cuisine. Un escalier, monte au premier @'etage.;^if (doll hasnt general) print  Des larmes descendent en cascades l'escalier, accompagnant les plaintes horribles d'Anastasia.;^w_to garden,^e_to kitchen,^u_to chamber;^";
                      if (location == chamber) print_ret "Room chamber^with description La chambre ne communique avec aucune pi@`ece @`a l'@'etage. Un petit brasero romain repose pr@`es du lit pour les nuits froides. Sur un gu@'eridon, un ensemble de th@'e de porcelaine chinoise au marli dor@'e de dragons d'Asie attend patiemment ses buveurs. Accot@'e sur le mur, un r@'ecamier poussi@'ereux donne l'impression d'une chambre abandonn@'ee. Sur une console, une torch@`ere aux bougies fondues n'@'eclaire plus depuis longtemps; et, tout pr@`es, un bateau de porcelaine repose. Malgr@'e tout, la chambre garde une impression a@'er@'ee, en raison d'un @oeil de b@oeuf qui compl@'etait le mur Ouest. De plus, le plafond se prolongeait en une coupole repr@'esentant un ciel bleu-poudre avec quelque nuages, et dont le tambour @'etait couvert de ch@'erubins. Pr@`es du lit, une
                     armoire au panneau de vantail sculpt@'e en visage antique compl@'etait le mobilier de la chambre.,^d_to lounge;";
                      if (location == cellar) print_ret "Bien essay@'e, mais je ne peux pas d@'evoiler tous mes secrets quand m@^eme!";
                                                    }
                      else { print_ret "Ah ou. C'est mieux ainsi, vous savez, de ne pas briser l'illusion, je veux dire.";   }
                  default: "@cCa y est maintenant, vous m'avez @'epuis@'e! Faites ce que vous avez @`a faire.";
                                                                  }
                 ],
          found_in [;  return true;  ],
          has scenery ~proper;

!--------------------------------Grammaire----------------------------------------------!

Include "FrenchG";

!-------------------------------Routines------------------------------------------------!

[ TheEnd;
     give fillette light;
     style underline;  print "Depuis ce jour la maison des pleurs est surnomm@'ee l'h@^otel Dieu, en raison du miracle survenus, et on peut y trouver dans le hall, pr@`es d'une peinture de style mystique o@`u ~la fille de Dieu~ est repr@'esent@'ee montant au ciel, une peinture d'un renard. Le bruit de l'@'ev@`enement courut tant et si bien que l'H@^otel Dieu est autant fr@'equent@'e par les p@`elerins que l'@'eglise de Sacerdoce. En retour, l'orphelinat ne manque plus d'argent, puisque les p@`elerins sont g@'en@'ereux et de bon c@oeur. Certainement aussi, les enfants sont plus visit@'es, et par cons@'equent, plus souvent adopt@'es. Et personne n'entendit jamais plus parler de la maison des pleurs.";
       ]; !déonnaire?

[ Apparition;
     print "Elle porte une robe rapiecees, trou@'ee par endroits et des sabots de bouleau us@'es. Elle a les cheveux blonds et boucl@'es, une bouche en c@oeur, les yeux brun et na@:if.";
     ];

!---------------------------------Verbes---------------------------------------------!

[ TalkSub;
 if (noun == player) print_ret "Rien d'inattendu.";
 if (RunLife(noun,##Talk) ~=false) return; !consult life[; Talk: ]
 print_ret "@`A ce moment, vous ne savez que dire.";
 ];

[ReadSub;
 "Vous lisez pour passer le temp.";
 ];

[HugSub;
 "Vous donnez un c@^alin.";
 ];

[CrySub;
 "Non, vraiment, vous n'y arrivez pas.";
 ];

Verb 'pleurer'
*                          -> Cry;

Verb 'reconforter'
* noun                -> Hug;

Verb 'caliner'
* noun                -> Hug;

Verb 'Consoler'
* noun                -> Hug;

Extend 'parler' replace
 * 'a'/'avec' creature -> Talk
 * creature                -> Talk;

Extend 'lire' first
*                         -> Read
* noun               -> Read;
